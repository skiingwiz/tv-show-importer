import java.text.SimpleDateFormat
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'org.hibernate.build.gradle', name: 'version-injection-plugin', version: '1.0.0'
    }
}

plugins {
    id 'java'
    id 'eclipse'
}
apply plugin: 'version-injection'

version = '4.0.0'
defaultTasks = ['clean','publish']

sourceSets {
    scripts
}

versionInjection {
    into( 'main.Version', 'getVersionString' )
}

dependencies {
   implementation group: 'commons-cli', name: 'commons-cli', version: '[1.3.1, 2)'
   implementation group: 'commons-io', name: 'commons-io', version: '[2.4, 3)'
   implementation group: 'org.slf4j', name: 'slf4j-api', version: '[1.7, 1.8)'
   implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '[4.5, 5)'
   implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '[2.10, 3)'

   runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '[1.2, 1.3)'
   runtimeOnly group: 'ch.qos.logback', name: 'logback-core', version: '[1.2, 1.3)'
   runtimeOnly group: 'org.codehaus.groovy', name: 'groovy', version: '[2.5, 2.6)'

   testImplementation group: 'junit', name: 'junit', version: '4.12+'
}

repositories {
   mavenCentral()
}

processScriptsResources {
    String jarName = tasks.jar.outputs.files.singleFile.name

    filter ReplaceTokens, tokens:[
        'jar.name': jarName,
    ]
}

task publish(type: Zip, dependsOn: [build, processScriptsResources]) {
    from tasks.jar.outputs.files
    from tasks.processScriptsResources.outputs.files
}

jar {
   manifest {
      attributes(
          'Main-Class': 'main.Main',
          'Built-By': System.properties['user.name'],
          'Built-At': now('dd-MMM-yyyy HH:mm:ss'),
          'Implementation-Version': version
      )
   }

   from {
      configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
   }

   duplicatesStrategy 'exclude'
}

private String now(String format) {
   return new SimpleDateFormat(format).format(new Date())
}
