import java.text.SimpleDateFormat
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java'
  id 'eclipse'
}

sourceSets {
    scripts
}

dependencies {
   compile group: 'commons-io', name: 'commons-io', version: '[2.4, 3)'

   testCompile group: 'junit', name: 'junit', version: '4.12+'
}

repositories {
   mavenCentral()
   jcenter()
}

version = '1.2.0'

processResources{
    filter ReplaceTokens, tokens:['version': version]
 }

processScriptsResources {
    String classpath = configurations.runtime*.name.join(':')
    String jarName = tasks.jar.outputs.files.singleFile.name
    
    filter ReplaceTokens, tokens:['jar.name': jarName, 
        'classpath': classpath]
}

task publish(type: Zip, dependsOn: [build, processScriptsResources]) {
    from tasks.jar.outputs.files
    from tasks.processScriptsResources.outputs.files
    from configurations.runtime
}

jar {
   manifest {
      attributes(
          'Main-Class': 'main.Main',
          'Built-By': System.properties['user.name'],
          'Built-At': now('dd-MMM-yyyy HH:mm:ss'),
          'Implementation-Version': version,
	  'Class-Path': configurations.runtime*.name.join(' ')
      )
   }
}

private String now(String format) {
   return new SimpleDateFormat(format).format(new Date())
}
